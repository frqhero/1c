&НаКлиенте
Процедура Команда1(Команда)
    
    Повторно = ЕстьРеквизитИлиСвойствоОбъекта();
    ВПоискНаСервере(Повторно);
    
КонецПроцедуры

&НаСервере
Процедура ВПоискНаСервере(Повторно)
    
    //БЛОК ПЕРЕМЕННЫХ ДАННЫХ ДЛЯ ОРГАНИЗАЦИИ ЗАПРОСА ++
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Счет КАК Счет,
	|	ХозрасчетныйОстатки.Субконто1 КАК Контрагент,
	|	ВЫРАЗИТЬ(ХозрасчетныйОстатки.Субконто2 КАК Справочник.ДоговорыКонтрагентов) КАК Договор,
	|	ХозрасчетныйОстатки.Субконто3 КАК Сделка,
	|	ХозрасчетныйОстатки.СуммаОстатокДт КАК СуммаОстаток,
	|	ХозрасчетныйОстатки.ВалютнаяСуммаОстатокДт КАК ВалютнаяСуммаОстаток
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&Период,
	|			Счет В (&СчетаРасчетовСАналитикойПоДокументам),
	|			&ВидыСубконтоСДокументами,
	|			Организация = &Организация
	|				И Субконто1 = &Контрагент
	|				И Субконто2 = &Договор
	|				И ТИПЗНАЧЕНИЯ(Субконто3) В (&ТипыДокументовРасчетов)) КАК ХозрасчетныйОстатки
	|ГДЕ
	|	(ВЫРАЗИТЬ(ХозрасчетныйОстатки.Субконто2 КАК Справочник.ДоговорыКонтрагентов).РасчетыВУсловныхЕдиницах
	|			ИЛИ ВЫРАЗИТЬ(ХозрасчетныйОстатки.Субконто2 КАК Справочник.ДоговорыКонтрагентов).ВалютаВзаиморасчетов В (&ВалютаРеглУчета))
	|	И ХозрасчетныйОстатки.СуммаОстатокДт > 0";
	
	СчетаУчетаРасчетов = БухгалтерскиеОтчеты.СчетаУчетаРасчетовПокупателей();	
	СчетаРасчетовБезАналитикиПоДокументам = СчетаУчетаРасчетов.СчетаБезДокументаРасчетов;
	СчетаРасчетовСАналитикойПоДокументам  = СчетаУчетаРасчетов.СчетаСДокументомРасчетов;
	ТабличнаяЧастьМетаданные = Метаданные.Документы.НачислениеПеней.ТабличныеЧасти.Задолженность.Реквизиты;
	СписокВалютыРеглУчета = Новый СписокЗначений;
	СписокВалютыРеглУчета.Добавить(Справочники.Валюты.НайтиПоНаименованию("руб."));
	СписокВалютыРеглУчета.Добавить(Справочники.Валюты.ПустаяСсылка());
	АналитикаРасчетов = Новый Массив;
	АналитикаРасчетов.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
	АналитикаРасчетов.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры);
	АналитикаРасчетов.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами);
    ВидыСубконтоСДокументами = АналитикаРасчетов;
	//ПАРАМЕТРЫ ЗАПРОСА++
	Запрос.УстановитьПараметр("Период", Дата(2021, 04, 02, 12, 55, 50));
	Запрос.УстановитьПараметр("СчетаРасчетовСАналитикойПоДокументам",  СчетаРасчетовСАналитикойПоДокументам);
	Запрос.УстановитьПараметр("Организация", Справочники.Организации.НайтиПоКоду("00-000010"));
	Запрос.УстановитьПараметр("Контрагент",  Справочники.Контрагенты.НайтиПоКоду("00-000284"));
	Запрос.УстановитьПараметр("Договор",     Справочники.ДоговорыКонтрагентов.НайтиПоКоду("00-000475"));
	Запрос.УстановитьПараметр("ТипыДокументовРасчетов", ТабличнаяЧастьМетаданные.Сделка.Тип.Типы());
	Запрос.УстановитьПараметр("ВалютаРеглУчета",  СписокВалютыРеглУчета);
	Запрос.УстановитьПараметр("ВидыСубконтоСДокументами", ВидыСубконтоСДокументами);
	//ПАРАМЕТРЫ ЗАПРОСА--
	
	//БЛОК ПЕРЕМЕННЫХ ДАННЫХ ДЛЯ ОРГАНИЗАЦИИ ЗАПРОСА --

        
    ТаблицаЗапроса = Запрос.Выполнить().Выгрузить();
    СоздатьТаблицуФормы(ТаблицаЗапроса, Повторно);
    
КонецПроцедуры

&НаСервере
Процедура СоздатьТаблицуФормы(ТаблицаЗапроса, Повторно)
    
    Если Не Повторно Тогда
        
        МассивТипаВыбора = Новый Массив;
        МассивТипаВыбора.Добавить(Тип("ТаблицаЗначений"));
        
        ОписаниеТипаВыбора = Новый ОписаниеТипов(МассивТипаВыбора);
        
        МассивРеквизитов = Новый Массив;
        МассивРеквизитов.Добавить(Новый РеквизитФормы("РезультатЗапроса", ОписаниеТипаВыбора, "", "ТЗН"));
        
        Для Каждого Колонка Из ТаблицаЗапроса.Колонки Цикл
            
            МассивРеквизитов.Добавить(Новый РеквизитФормы(Колонка.Имя, Колонка.ТипЗначения, "РезультатЗапроса"));
            
        КонецЦикла;
        ИзменитьРеквизиты(МассивРеквизитов);
        
        ТаблицаПолейВыбора = Элементы.Добавить("ТЗН", Тип("ТаблицаФормы"));
        ТаблицаПолейВыбора.ПутьКДанным = "РезультатЗапроса";
        ТаблицаПолейВыбора.Отображение = ОтображениеТаблицы.Список;
        
        Для Каждого Колонка Из ТаблицаЗапроса.Колонки Цикл
            
            НовыйЭлемент = Элементы.Добавить(Колонка.Имя, Тип("ПолеФормы"), ТаблицаПолейВыбора);
            НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
            НовыйЭлемент.ПутьКДанным = "РезультатЗапроса." + Колонка.Имя;
            
        КонецЦикла;
        
    КонецЕсли;
    
    ЗначениеВРеквизитФормы(ТаблицаЗапроса, "РезультатЗапроса");
    
КонецПроцедуры

&НаКлиенте
Функция ЕстьРеквизитИлиСвойствоОбъекта()
    
    КлючУникальности   = Новый УникальныйИдентификатор;
    СтруктураРеквизита = Новый Структура("РезультатЗапроса", КлючУникальности);
    ЗаполнитьЗначенияСвойств(СтруктураРеквизита, ЭтотОбъект);
    
    Возврат СтруктураРеквизита["РезультатЗапроса"] <> КлючУникальности;
    
КонецФункции
